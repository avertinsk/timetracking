MODULE Category;

REQUIRE Employee, Project;

CLASS Category 'Категория';
TABLE category (Category);

name 'Название' = DATA STRING[50] (Category) CHARWIDTH 15;
rate 'Ставка, руб.' = DATA NUMERIC[14,2] (Category);

FORM category 'Категория'
    OBJECTS c = Category PANEL
    PROPERTIES(c) name, rate
    
    EDIT Category OBJECT c
;

FORM categories 'Категории'
    OBJECTS c = Category
    PROPERTIES(c) READONLY name, rate
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE
;

FORM dialogCategory 'Выбор категории'
    OBJECTS c = Category
    PROPERTIES(c) READONLY name
    
    LIST Category OBJECT c
;

NAVIGATOR {
    masterData {
        NEW categories;
    }
}

category 'Категория' = DATA Category (Employee);
nameCategory 'Категория' (Employee e) = name(category(e));

EXTEND FORM employee
    PROPERTIES(e) nameCategory
;

EXTEND FORM employees
    PROPERTIES(e) READONLYIF isReadonly() nameCategory
;

hoursMonthly 'Часов в абонентской плате' = DATA NUMERIC[14,2] (Project);
sumMonthly 'Сумма абонентской платы' = DATA NUMERIC[14,2] (Project);

TABLE project(Project, Category);
dataRate 'Ставка, руб.' = DATA NUMERIC[14,2] (Project, Category);
rate 'Ставка, руб.' (Project p, Category cc) = OVERRIDE dataRate(p, cc), (rate(cc) IF p IS Project); 

EXTEND FORM customer
    PROPERTIES(p) PANEL hoursMonthly, sumMonthly

    OBJECTS cc = Category
    PROPERTIES name(cc) READONLY, rate(p, cc) BACKGROUND dataRate(p, cc)
;

DESIGN customer {
    tabPane {
        NEW rates FIRST {
            caption = 'Оплата';
            NEW header {
                type = CONTAINERH;
                MOVE PROPERTY(hoursMonthly(p));
                MOVE PROPERTY(sumMonthly(p));
            }
            MOVE BOX(cc);
        }
    }
}

category = DATA Category(Employee, Project);
nameCategory 'Категория' (Employee e, Project p) = name(category(e, p));

overCategory (Employee e, Project p) = OVERRIDE category(e, p), category(e);
nameOverCategory 'Категория' (Employee e, Project p) = name(overCategory(e, p));

EXTEND FORM customer 
    OBJECTS e = Employee
    PROPERTIES name(e) READONLY, nameCategory(e, p) 
    FILTERS assigned(p, e)
    FILTERGROUP category FILTER 'По категории' category(e) == cc
    FILTERGROUP cat FILTER 'Неосновные категории' NOT category(e) == category(e, p)
;

DESIGN customer {
    rates {
        MOVE BOX (e) AFTER BOX (cc);
    }
}