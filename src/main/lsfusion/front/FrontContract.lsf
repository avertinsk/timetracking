MODULE FrontContract;

REQUIRE FrontSession, EmploymentContract;

NAMESPACE Front;

filterPerson = DATA LOCAL Person();
filterLegalEntity = DATA LOCAL LegalEntity();

FORM exportContracts
    OBJECTS c = Customer PANEL 
    FILTERS customer(filterLegalEntity()) == c OR NOT filterLegalEntity(), customer(filterPerson()) == c OR NOT filterPerson()
    
    OBJECTS ec = EmploymentContract
    PROPERTIES IN public id = LONG (ec), idDocument = idDocument(ec), date = date(ec), city = city(ec), positionEmployee = positionEmployee(ec), 
                         dateFrom = dateFrom(ec), duration = duration(ec), salary = salary(ec), 
                         idPerson = LONG (person(ec) AS Person), idLegalEntity = LONG (legalEntity(ec) AS LegalEntity)
    FILTERS legalEntity(ec) == filterLegalEntity() OR NOT filterLegalEntity(), person(ec) == filterPerson() OR NOT filterPerson()
    
;

getContracts(TEXT token, FILE f) {
    
    checkToken(token);
    IF invalidToken() THEN {
        RETURN;
    }
    
    LOCAL idLegalEntity = LONG ();
    LOCAL idPerson = LONG ();
    
    IMPORT JSON FROM f AS FILE TO() idLegalEntity = idLegalEntity, idPerson = idPerson;
    
    filterPerson() <- GROUP MAX Person p IF LONG (p AS Person) == idPerson();
    filterLegalEntity() <- GROUP MAX LegalEntity p IF LONG (p AS LegalEntity) == idLegalEntity();
    
    IF idPerson() AND NOT filterPerson() THEN {
        EXPORT JSON FROM code = '404', message = 'Неверный id физ.лица';
        RETURN;
    }
    IF idLegalEntity() AND NOT filterLegalEntity() THEN {
        EXPORT JSON FROM code = '404', message = 'Неверный id юр.лица';
        RETURN;
    }
    IF filterLegalEntity() OR filterPerson() THEN {
        EXPORT exportContracts OBJECTS c = customer(frontUser(frontSession(token))) JSON;
    }
}

deleteContract(TEXT token, FILE f) {
        
    checkToken(token);
    IF invalidToken() THEN {
        RETURN;
    }
    
    LOCAL id = LONG ();
    
    IMPORT JSON FROM f AS FILE TO() id = id;
    
    FOR LONG (EmploymentContract c AS EmploymentContract) == id() DO {
        DELETE c;
    }
    APPLY;
    
    EXPORT JSON FROM code = '0', message = 'OK';
}

FORM exportContract
    OBJECTS c = EmploymentContract PANEL 
    PROPERTIES IN public id = LONG (c), idLegalEntity = LONG (legalEntity(c)), idPerson = LONG (person(c)), idDocument = idDocument(c), code = '0',
                         date = date(c), city = city(c), positionEmployee = positionEmployee(c), dateFrom = dateFrom(c), duration = duration(c), salary = salary(c)
;

getContract(TEXT token, FILE f) {
    
    checkToken(token);
    IF invalidToken() THEN {
        RETURN;
    }
    
    LOCAL id = LONG ();
    
    IMPORT JSON FROM f AS FILE TO() id = id;
    
    FOR LONG (EmploymentContract c AS EmploymentContract) == id() DO {
        EXPORT exportContract OBJECTS c = c JSON;
        RETURN;
    }
    
    EXPORT JSON FROM code = '404', message = 'Неверный id';
}

idContract = DATA LOCAL LONG ();
idLegalEntity = DATA LOCAL LONG ();
idPerson = DATA LOCAL LONG ();
idDocument = DATA LOCAL VARSTRING[256]();
date = DATA DATE ();
city = DATA VARSTRING[256]();
positionEmployee = DATA VARSTRING[1024]();
dateFrom = DATA DATE ();
duration = DATA INTEGER ();
salary = DATA NUMERIC[15,2]();

FORM importContract
    PROPERTIES id = idContract(), idLegalEntity = idLegalEntity(), idPerson = idPerson(), idDocument = idDocument(), 
               date = date(), city = city(), positionEmployee = positionEmployee(), dateFrom = dateFrom(), duration = duration(), salary = salary()
;

addContract(TEXT token, FILE f) {
      
    checkToken(token);
    IF invalidToken() THEN {
        RETURN;
    }
    
    fileToString(f, 'UTF-8');
    logToFile('API', 'addContract:\n' + resultString());
    
    IMPORT importContract JSON FROM f AS FILE;

    IF idContract() AND NOT (GROUP MAX EmploymentContract c AS EmploymentContract IF LONG (c) == idContract()) THEN {
        EXPORT JSON FROM code = '404', message = 'Не найден договор по коду';
        RETURN;
    }
    
    IF NOT idContract() THEN NEW c = EmploymentContract {
        idContract() <- LONG (c);
    }
    
    FOR LONG (EmploymentContract c AS EmploymentContract) == idContract() DO {
        legalEntity(c) <- GROUP MAX LegalEntity b AS LegalEntity IF LONG (b) == idLegalEntity();
        person(c) <- GROUP MAX Person b AS Person IF LONG (b) == idPerson();
        idDocument(c) <- idDocument();
        date(c) <- date();
        city(c) <- city();
        positionEmployee(c) <- positionEmployee();
        dateFrom(c) <- dateFrom();
        duration(c) <- duration();
        salary(c) <- salary();
    }
    APPLY NESTED (idContract[]);
    EXPORT JSON FROM id = idContract(), code = '0';
}

generateFileContract (TEXT token, FILE f) {
    
    checkToken(token);
    IF invalidToken() THEN {
        RETURN;
    }
    
    LOCAL id = LONG ();
    
    IMPORT JSON FROM f AS FILE TO() id = id;
    
    FOR LONG (EmploymentContract c AS EmploymentContract) == id() DO {
        createTemplateEntries(c);
        process(defaultEmplomentContractTemplate());
        exportFile() <- FILE (resultTemplate());
        RETURN;
    }
    
    EXPORT JSON FROM code = '404', message = 'Не найден договор по коду';
}