MODULE Invoice;

REQUIRE SystemEvents, Customer, Employee, Category,
        TimeEntry, 
        Numerator, Utils;

CLASS Invoice 'Счет';
TABLE invoice (Invoice);

project 'Проект' = DATA Project (Invoice) NONULL;
nameProject 'Проект' (Invoice i) = name(project(i));

customer 'Клиент' (Invoice i) = customer(project(i));
nameCustomer 'Клиент' (Invoice i) = name(customer(i));

date 'Дата' = DATA DATE (Invoice) NONULL;
date(i) <- currentDate() WHEN SET(i IS Invoice);

@defineNumbered(Invoice);
@defineNumeratedDefault(Invoice, 'Счета', '');

author 'Автор' = DATA Employee (Invoice);
nameAuthor 'Автор' (Invoice i) = name(author(i));

// Время выполнения

invoice 'Счет' = DATA Invoice (TimeEntry) INDEXED;

CONSTRAINT project(TimeEntry t) != project(invoice(t)) MESSAGE 'Проект счета не соответствует проекту отметки времени'; 

fromDate 'Дата с' = DATA DATE (Invoice);
toDate 'Дата по' = DATA DATE (Invoice);

changeInvoiceDates = DATA BOOLEAN ();
onStarted() + {
    IF NOT changeInvoiceDates() THEN {
        fromDate(Invoice i) <- GROUP MIN dateStart(TimeEntry e) IF invoice(e) == i;
        toDate(Invoice i) <- lastDayOfMonth(fromDate(i));
        changeInvoiceDates() <- TRUE;
    }
}

fromDate(Invoice i) <- (GROUP MIN dateStart(TimeEntry e) IF invoice(e) == i) WHEN SET (i IS Invoice);

WHEN LOCAL CHANGED (fromDate(Invoice i)) AND NOT CHANGED (toDate(i)) DO {
    toDate(i) <- lastDayOfMonth(fromDate(i));
}

TABLE invoiceTask (Invoice, Task);
fromDate 'Дата с' = GROUP MIN dateStart(TimeEntry e) BY invoice(e), task(e) MATERIALIZED;

hours 'Часов' = GROUP SUM NUMERIC[5,1](hours(TimeEntry e)) BY invoice(e) MATERIALIZED;
hours 'Часов' (Invoice i, Task t) = GROUP SUM NUMERIC[5,1](hours(TimeEntry e)) BY invoice(e), task(e) MATERIALIZED;
hours 'Часов' (Invoice i, Category c) = GROUP SUM NUMERIC[5,1](hours(TimeEntry e)) BY invoice(e), category(task(e)) MATERIALIZED;

rate 'Ставка, руб.' = DATA NUMERIC[14,2] (Invoice, Task);
rate(Invoice i, Task t) <- rate(project(i), category(t)) WHEN SET(hours(i, t)) OR CHANGED(category(t));

hoursMonthly 'Часов в абонентской плате' = DATA NUMERIC[14,2] (Invoice);
hoursMonthly (Invoice i) <- hoursMonthly(project(i)) WHEN CHANGED(project(i));

sumMonthly 'Сумма абонентской платы' = DATA NUMERIC[14,2] (Invoice);
sumMonthly (Invoice i) <- sumMonthly(project(i)) WHEN CHANGED(project(i));

sum 'Сумма, руб.' (Invoice i, Task t) = hours(i, t) * rate(i, t);
sum 'Сумма, руб.' (Invoice i) = GROUP SUM sum(i, Task t);

hoursMonthly 'Часов в абонентской плате'  = DATA NUMERIC[14,1](Project, Category);
hoursMonthly 'Часов в абонентской плате' (Invoice i, Category c) = hoursMonthly(project(i), c);

hoursMonthlyCategory 'Часов в абонентской плате по категориям' (Invoice i) = GROUP SUM hoursMonthly(i, Category c);

EXTEND FORM customer PROPERTIES hoursMonthly(p, cc);

hoursPayCategory (Invoice i, Task t) = hours(i,t) (-) PARTITION UNGROUP hoursMonthly LIMIT hours(i,t) ORDER rate(i,t), fromDate(i,t), t BY i, category(t);
hoursPay 'Часов к оплате' (Invoice i, Task t) = hoursPayCategory(i,t) (-) PARTITION UNGROUP hoursMonthly LIMIT hoursPayCategory(i,t) ORDER rate(i,t), fromDate(i,t), t BY i;
hoursPay 'Часов к оплате' (Invoice i) = GROUP SUM hoursPay(i, Task t);
hoursPay 'Часов к оплате' (Invoice i, Category c) = GROUP SUM hoursPay(i, Task t) BY category(t);

sumPay 'Сумма к оплате' (Invoice i, Task t) = hoursPay(i,t) * rate(i, t);
sumPay 'Сумма к оплате' (Invoice i) = GROUP SUM sumPay(i, Task t);
sumPay 'Сумма к оплате' (Invoice i, Category c) = hoursPay(i,c) * rate(project(i), c);

CONSTRAINT extractMonth(fromDate(Invoice i)) != extractMonth(toDate(i)) AND hoursMonthly(i) MESSAGE 'Запрещено выставлять счет за несколько месяцев одновременно';

removeInvoice 'Исключить' (TimeEntry e)  { 
    invoice(e) <- NULL;
}

codeEmployee 'Код' (Task t) = code(employee(t));
nameCategoryEmployee 'Категория сотрудника' (Task t) = nameCategory(employee(t));

FORM invoice 'Счет'
    OBJECTS i = Invoice PANEL
    PROPERTIES(i) date, nameNumerator, series, number, nameCustomer, nameProject, fromDate, toDate, hours, hoursMonthly, hoursPay, sum, sumMonthly, sumPay
    
    OBJECTS t = Task
    PROPERTIES(t) READONLY nameProject, codeEmployee, nameCategoryEmployee, nameEmployee, nameTaskType
    PROPERTIES(i, t) fromDate READONLY, hours READONLY, hoursPay READONLY, rate, sum READONLY, sumPay READONLY
    PROPERTIES(t) READONLY name, description PANEL
    FILTERS hours(i, t),
            assigned(project(t), currentEmployee())
    
    OBJECTS e = TimeEntry
    PROPERTIES(e) READONLY nameActivity, dateTimeStart, hours BACKGROUND userHours(e), dateTimeFinish
    PROPERTIES(e) removeInvoice TOOLBAR
    FILTERS task(e) == t
    
    OBJECTS c = Category
    PROPERTIES READONLY name(c), hoursMonthly(i, c), hoursPay(i, c)
    FILTERS hoursMonthly(i, c) OR hoursPay(i, c)
    ORDER hoursMonthly(i, c) DESC 
    
    EDIT Invoice OBJECT i
;

DESIGN invoice {
    NEW header FIRST {
        caption = 'Счет';
        NEW params {
            type = CONTAINERH;
            MOVE PROPERTY(date(i));
            MOVE PROPERTY(nameNumerator(i));
            MOVE PROPERTY(series(i));
            MOVE PROPERTY(number(i));
            MOVE PROPERTY(nameCustomer(i));
            MOVE PROPERTY(nameProject(i));
        }
        NEW time {
            type = CONTAINERH;
            MOVE PROPERTY(fromDate(i));
            MOVE PROPERTY(toDate(i));
            MOVE PROPERTY(hours(i));
            MOVE PROPERTY(hoursMonthly(i));
            MOVE PROPERTY(hoursPay(i));
            MOVE PROPERTY(sum(i));
            MOVE PROPERTY(sumMonthly(i));
            MOVE PROPERTY(sumPay(i));
        }
    }
    NEW pane AFTER header {
        fill = 1;
        type = SPLITH;
        MOVE BOX(t) { 
            fill = 3;
            MOVE PROPERTY(description(t)) {
                fill = 0.5;
                panelCaptionAbove = TRUE;
            } 
        }
        NEW right {
            fill = 1;
            MOVE BOX(e);
            MOVE BOX (c);
        }
    }
}

backgroundCategory (Task t) = CASE 
    WHEN NOT category(t) == category(employee(t)) AND category(t) == category(employee(t), project(t)) THEN RGB (255, 255, 200)
    WHEN NOT category(t) == (OVERRIDE category(employee(t), project(t)), category(employee(t))) THEN RGB (255, 200, 200);

changeCategoryProject(Invoice i, Task t) {
    DIALOG dialogCategory OBJECTS c INPUT NULL DO {
        category(Employee e, Project p) <- c WHERE e = employee(t) AND p == project(t);
        
        ASK 'Изменить категорию для всех задач в счёте?' DO {
            category(Task tt) <- (OVERRIDE c, category(employee(tt))) WHERE hours(i, tt) AND employee(tt) == employee(t);
        }
    }
}

EXTEND FORM invoice 
    PROPERTIES nameCategory(t) BACKGROUND backgroundCategory(t), 'Категория на проекте' = nameCategory(employee(t), project(t)) ON CHANGE changeCategoryProject(i, t) PANEL DRAW t TOOLBAR 
;

hours 'Часов' (Project p, Month m, INTEGER y) = GROUP SUM NUMERIC[5,1](hours(TimeEntry e)) BY project(e), extractMonth(dateStart(e)), extractYear(dateStart(e));
hours 'Часов' (Project p, Category c, Month m, INTEGER y) = GROUP SUM NUMERIC[5,1](hours(TimeEntry e)) BY project(e), overCategory(employee(e), project(e)), extractMonth(dateStart(e)), extractYear(dateStart(e));

checkHoursLimit 'Проверять приближение к лимиту часов' = DATA BOOLEAN ();

EXTEND FORM options PROPERTIES checkHoursLimit();
DESIGN options {
    commons {
        MOVE PROPERTY (checkHoursLimit());
    }
}

sendLimitEmails 'Получать уведомления о приближении к лимиту часов' = DATA BOOLEAN (Employee, Project);

EXTEND FORM customer PROPERTIES sendLimitEmails(e, p);

sent90 = DATA BOOLEAN (Project, Month, INTEGER);
sent95 = DATA BOOLEAN (Project, Month, INTEGER);
sent100 = DATA BOOLEAN (Project, Month, INTEGER);

sendEmailLimitHoursProjects 'Разослать письма о приближении к лимиту по проекту' () {
    
    LOCAL toSend = BOOLEAN (Project);
    
    toSend (Project p) <- (NOT sent90(p, extractMonth(currentDate()), currentYear()) AND round2(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p)) >= 0.90) OR 
                          (NOT sent95(p, extractMonth(currentDate()), currentYear()) AND round2(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p)) >= 0.95) OR 
                          (NOT sent100(p, extractMonth(currentDate()), currentYear()) AND round2(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p)) >= 1.0);
        
    FOR toSend(Project p) AND assigned(p, Employee e) AND sendLimitEmails(e, p) DO {
        EMAIL 
            SUBJECT CONCAT ' ', nameCustomer(p) + ':', 'израсходовано ' + 
                round0(hours(p,  extractMonth(currentDate()), currentYear())/hoursMonthly(p) * 100) + '% времени, включённого в оплату'
            TO email(e)
            BODY 'Здравствуйте!<br><br>Обратите внимание что у клиента ' + nameCustomer(p) + ' израсходовано ' + 
                round0(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p) * 100) + 
                '% времени, включённого в оплату в текущем периоде.<br><br>С уважением,<br><br>Ваша CRM система!';
    }
    sent90(Project p, Month m, INTEGER y) <- round2(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p)) >= 0.90 WHERE m = extractMonth(currentDate()) AND y = currentYear();
    sent95(Project p, Month m, INTEGER y) <- round2(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p)) >= 0.95 WHERE m = extractMonth(currentDate()) AND y = currentYear();
    sent100(Project p, Month m, INTEGER y) <- round2(hours(p, extractMonth(currentDate()), currentYear())/hoursMonthly(p)) >= 1.0 WHERE m = extractMonth(currentDate()) AND y = currentYear();
    APPLY;
}

sent90 = DATA BOOLEAN (Project, Category, Month, INTEGER);
sent95 = DATA BOOLEAN (Project, Category, Month, INTEGER);
sent100 = DATA BOOLEAN (Project, Category, Month, INTEGER);

sendEmailLimitHoursCategories 'Разослать письма о приближении к лимиту по категории' () {
    
    LOCAL toSend = BOOLEAN (Project, Category);
    
    toSend (Project p, Category c) <- (NOT sent90(p, c, extractMonth(currentDate()), currentYear()) AND round2(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c)) >= 0.90) OR 
                                      (NOT sent95(p, c, extractMonth(currentDate()), currentYear()) AND round2(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c)) >= 0.95) OR 
                                      (NOT sent100(p, c, extractMonth(currentDate()), currentYear()) AND round2(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c)) >= 1.0);
        
    FOR toSend(Project p, Category c) AND assigned(p, Employee e) AND sendLimitEmails(e, p) DO {
        EMAIL 
            SUBJECT CONCAT ' ', nameCustomer(p) + ':', name(c) + ':', 'израсходовано ' + 
                round0(hours(p,  c, extractMonth(currentDate()), currentYear())/hoursMonthly(p) * 100) + '% времени, включённого в оплату'
            TO email(e)
            BODY 'Здравствуйте!<br><br>Обратите внимание что у клиента ' + nameCustomer(p) + ' израсходовано ' + 
                round0(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p) * 100) + 
                '% времени специалиста ' + name(c) + ', включённого в оплату в текущем периоде.<br><br>С уважением,<br><br>Ваша CRM система!';
    }
    sent90(Project p, Category c, Month m, INTEGER y) <- round2(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c)) >= 0.90 WHERE m = extractMonth(currentDate()) AND y = currentYear(); 
    sent95(Project p, Category c, Month m, INTEGER y) <- round2(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c)) >= 0.95 WHERE m = extractMonth(currentDate()) AND y = currentYear();
    sent100(Project p, Category c, Month m, INTEGER y) <- round2(hours(p, c, extractMonth(currentDate()), currentYear())/hoursMonthly(p, c)) >= 1.0 WHERE m = extractMonth(currentDate()) AND y = currentYear();
    APPLY;
}

sendEmailLimitHours 'Разослать письма о приближении к лимиту' () {
    sendEmailLimitHoursProjects();
    sendEmailLimitHoursCategories();
}
