MODULE Project;

REQUIRE Customer, Employee;

CLASS Project 'Проект';
TABLE project(Project);

name 'Наименование' = DATA ISTRING[200] (Project) CHARWIDTH 15;

customer 'Клиент' = DATA Customer (Project) NONULL;
nameCustomer 'Клиент' (Project p) = name(customer(p));

WHEN LOCAL SET(Customer c IS Customer) DO {
    NEW p = Project {
        customer(p) <- c;
    }
}

CLASS ProjectType 'Тип проекта' {
    type1 'значение №1',
    type2 'значение №2',
    type3 'значение №3'
}

name 'Тип проекта' (ProjectType t) = staticCaption(t);

type 'Тип проекта' = DATA ProjectType (Project);
nameType 'Тип проекта' (Project p) = name(type(p));

FORM ProjectTypes 'Типы проектов'
    OBJECTS t = ProjectType
    PROPERTIES READONLY name(t)
    
    LIST ProjectType OBJECT t;
;

EXTEND FORM customer
    OBJECTS p = Project
    PROPERTIES(p) name, nameType, NEW, DELETE
    FILTERS customer(p) == c
;

DESIGN customer {
    leftPane {
        MOVE BOX(p);
    }
}

FORM dialogProject 'Выбор проекта'
    OBJECTS p = Project
    PROPERTIES(p) READONLY nameCustomer, name
    
    FILTERGROUP active FILTER 'Активный' active(customer(p)) 'F5' DEFAULT
    
    LIST Project OBJECT p; 
;  

// ---------- Назначение пользователей ----------- //

TABLE projectEmployee (Project, Employee);
assigned 'Вкл' = DATA BOOLEAN (Project, Employee);

assigned 'Вкл' (Customer c, Employee e) = GROUP SUM 1 IF assigned(Project p, e) BY customer(p);  

EXTEND FORM employee
    OBJECTS p = Project
    
    PROPERTIES assigned(p, e)
    PROPERTIES(p) READONLY nameCustomer, name
    ORDER nameCustomer(p), name(p)
    
    FILTERGROUP active FILTER 'Активный' active(customer(p)) 'F5' DEFAULT
;

DESIGN employee {
    OBJECTS {
        MOVE BOX(p) AFTER BOX(e);
    }
}

responsible = DATA Employee (Project);
nameResponsible 'Ответственный за проект' (Project p) = CONCAT ', ', lastName(responsible(p)), firstName(responsible(p));

changeResponsible (Project p) {
    DIALOG dialogEmployee OBJECTS e INPUT DO {
        responsible(p) <- e;
    }
}

FORM projects 'Проекты'
    OBJECTS p = Project
    PROPERTIES(p) nameCustomer, name, nameType, nameResponsible ON CHANGE changeResponsible(p)
    PROPERTIES(p) NEW, DELETE
;

NAVIGATOR {
    masterData {
        NEW projects;
    }
}