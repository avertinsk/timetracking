MODULE TimeReports;

REQUIRE TimeEntry, Project, Reports;

NAMESPACE Time;

hours 'Часов' (DATE d) = GROUP SUM hours(TimeEntry e) BY dateStart(e);

hours 'Часов' (Project p, DATE d) = GROUP SUM hours(TimeEntry e) BY project(e), dateStart(e);
hours 'Часов (всего)' (Project p, DATE dFrom, DATE dTo) = 
    GROUP SUM hours(TimeEntry e) IF dFrom <= dateStart(e) AND dTo >= dateStart(e) BY project(e); 

header (DATE d) = STRING[20] (d AS DATE);

FORM projects 'По проектам'
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES dFrom = VALUE(dFrom), dTo = VALUE(dTo)

    OBJECTS d = DATE 
    FILTERS hours(d), d >= dFrom, d <= dTo

    OBJECTS p = Project
    PROPERTIES(p) READONLY nameCustomer, name
    PROPERTIES READONLY hours(p, dFrom, dTo)
    FILTERS assigned(p, currentEmployee())
    ORDER nameCustomer(p)
    FILTERGROUP active FILTER 'Активный' active(customer(p)) 'F5' DEFAULT
    
    PROPERTIES READONLY hours(p, d) COLUMNS (d) HEADER header(d)
    
    OBJECTS t = TimeEntry
    PROPERTIES(t) READONLY nameTaskType, nameTask, namePractice, nameEmployee
    PROPERTIES(t) descriptionTask ON CHANGE changeDescriptionTask(t) PANEL
    PROPERTIES(t) nameActivity, dateTimeStart, dateTimeFinish, hours BACKGROUND userHours(t), comment PANEL
    FILTERS project(t) == p,
            dateStart(t) >= dFrom AND dateStart(t) <= dTo 
;

DESIGN projects {
    OBJECTS {
        NEW timeEntry {
            fill = 2;
            type = SPLITH;
            MOVE BOX(t) {
                fill = 3;
            }
            NEW description {
                MOVE PROPERTY(descriptionTask(t)) {
                    panelCaptionAbove = TRUE;
                    fill = 1;
                }
                MOVE PROPERTY(comment(t)) {
                    panelCaptionAbove = TRUE;
                    fill = 1;
                }
            }
        }
    }
}


hours 'Часов' (Employee e, DATE d) = GROUP SUM hours(TimeEntry te) BY employee(te), dateStart(te);
hours 'Часов (всего)' (Employee e, DATE dFrom, DATE dTo) = 
    GROUP SUM hours(TimeEntry te) IF dFrom <= dateStart(te) AND dTo >= dateStart(te) BY employee(te); 

FORM employees 'По сотрудникам'
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES dFrom = VALUE(dFrom), dTo = VALUE(dTo)

    OBJECTS d = DATE 
    FILTERS hours(d), d >= dFrom, d <= dTo

    OBJECTS e = Employee
    PROPERTIES(e) READONLY namePractice, firstName, lastName, login
    PROPERTIES READONLY hours(e, dFrom, dTo)
    
    PROPERTIES READONLY hours(e, d) COLUMNS (d) HEADER header(d)
    
    OBJECTS t = TimeEntry
    PROPERTIES(t) READONLY nameCustomerProject, nameProject, nameTaskType, nameTask
    PROPERTIES(t) descriptionTask ON CHANGE changeDescriptionTask(t) PANEL
    PROPERTIES(t) nameActivity, dateTimeStart, dateTimeFinish, hours BACKGROUND userHours(t), comment PANEL
    FILTERS employee(t) == e,
            dateStart(t) >= dFrom AND dateStart(t) <= dTo,
            assigned(project(t), currentEmployee())
;

DESIGN employees {
    OBJECTS {
        NEW timeEntry {
            fill = 2;
            type = SPLITH;
            MOVE BOX(t) {
                fill = 3;
            }
            NEW description {
                MOVE PROPERTY(descriptionTask(t)) {
                    panelCaptionAbove = TRUE;
                    fill = 1;
                }
                MOVE PROPERTY(comment(t)) {
                    panelCaptionAbove = TRUE;
                    fill = 1;
                }
            }
        }
    }
}

NAVIGATOR {
    reports {
        NEW projects;
        NEW employees; 
    }
}

dataHoursSet 'Кол-во установленных рабочих часов' = DATA NUMERIC[10,2] () CHARWIDTH 5;
sendNotification 'Отправлять уведомление' = DATA BOOLEAN (Employee);

EXTEND FORM options
    PROPERTIES dataHoursSet()
    
    OBJECTS e = Employee
    PROPERTIES(e) READONLY firstName, lastName, login, nameMainRole, email
    PROPERTIES sendNotification(e)
;

DESIGN options {
    pane {
        NEW employeeMailing {
            caption = 'Рассылка для сотрудников';
            MOVE PROPERTY(dataHoursSet()) { flex = 0; align = START; }
            MOVE BOX(e);
        }
    }
}

isDayoff 'Выходной' = DATA BOOLEAN (DATE);

FORM workDays 'Производственный календарь'
    OBJECTS df = DATE PANEL, dt = DATE PANEL
    PROPERTIES dFrom 'Дата с' = VALUE(df), dTo 'по' = VALUE(dt)
    
    OBJECTS d = DATE 
    PROPERTIES date 'Дата' = VALUE(d), extractMonthName(d), extractDOWName(d), isDayoff(d)
    FILTERS iterate(d, df, dt)
;

DESIGN workDays {
    OBJECTS {
        NEW header {
            type = CONTAINERH;
            MOVE PROPERTY(dFrom);
            MOVE PROPERTY(dTo);
        }
        MOVE BOX(d);
    }
}

NAVIGATOR {
    masterData {
        NEW workDays;
    }
}

hoursSet 'Кол-во установленных рабочих часов' () = OVERRIDE dataHoursSet(), 6.0;
prevWorkDate (DATE d) = GROUP LAST DATE dd IF NOT isDayoff(dd) AND iterate(dd, 2020_01_01, sum(d, -1)) ORDER dd;

sendNotifications 'Отправить уведомление' () {
    IF NOT isDayoff(currentDate()) THEN {
        
        FOR sendNotification(Employee e) AND (hours(e, prevWorkDate(currentDate())) < hoursSet() OR NOT hours(e, prevWorkDate(currentDate()))) AND email(e) DO {
            EMAIL
                SUBJECT 'Напоминание о дополнительном внесении времени'
                TO email(e)
                BODY firstName(e) + ', привет!<br /><br />Пожалуйста, проверьте, и, если нужно, откорректируйте количество учтённого времени за ' 
                    + prevWorkDate(currentDate()) + '<br /><br />Я тут заметила, что ' + prevWorkDate(currentDate()) + 
                    ' учтёно времени меньше обычного. Мне очень-очень важно знать распределение вашего времени. Это поможет мне автоматизировать процессы и тем самым убрать скучную рутину из вашей работы.<br />' +
                    'А для этого мне нужно понимать как распределено ваше время. Воможно вы закрутились и забыли учесть часть времени?<br /><br />' +
                    'Поэтому, буду очень благодарна, если вы сможете проверить, и, если нужно, откорректировать количество учтённого времени за ' +
                    prevWorkDate(currentDate()) + '<br /><br />' + 'Катерина,<br /> Ваша CRM система'
            ;
        }
    }
}

EXTEND FORM options
    PROPERTIES sendNotifications() DRAW e TOOLBAR
;

sendNotificationManager 'Уведомления для руководителя' = DATA BOOLEAN (Employee);
dataNotificationPeriod 'Период уведомлений для руководителя, дни' = DATA INTEGER ();
notificationPeriod () = OVERRIDE dataNotificationPeriod(), 7;

isManager (Employee manager) = GROUP SUM 1 IF managers(Employee employee, manager);

EXTEND FORM options
    PROPERTIES sendNotificationManager(e) READONLYIF NOT isManager(e), dataNotificationPeriod()
;

DESIGN options {
    employeeMailing {
        MOVE PROPERTY(dataNotificationPeriod()) AFTER PROPERTY(dataHoursSet()) { flex = 0; align = START; }
    }
}


isTimeShortage (Employee e) = 
    GROUP SUM 1 IF (hours(e, DATE d) < hoursSet() OR NOT hours(e, d)) AND NOT isDayoff(d) AND iterate(d, sum(currentDate(), -1 * notificationPeriod()), sum(currentDate(), -1));
isTimeShortageEmployees (Employee manager) = GROUP SUM 1 IF managers(Employee e, manager) AND isTimeShortage(e);
timeShortageNumeric (Employee e, DATE d) = hoursSet() (-) hours(e, d);
timeShortageString (Employee e, DATE d) = CONCAT '', INTEGER(trunc(timeShortageNumeric(e, d), 0)), ' час(ов) ', 
    round0(60 * (timeShortageNumeric(e, d) - INTEGER(trunc(timeShortageNumeric(e, d), 0)))), ' минут(а)';
    
messageManager = DATA LOCAL STRING (Employee);

sendNotificationsManager 'Уведомления для руководителя' () {
    IF NOT isDayoff(currentDate()) THEN {
        
        FOR sendNotificationManager(Employee e) AND isManager(e) AND email(e) AND isTimeShortageEmployees(e) DO {
            FOR managers(Employee employee, e) AND isTimeShortage(employee) DO {
                messageManager(employee) <- NULL;
                messageManager(employee) <- CONCAT '', lastName(employee), ' ', firstName(employee), '<br />';
                FOR iterate(DATE d, sum(currentDate(), -1 * notificationPeriod()), sum(currentDate(), -1)) AND NOT isDayoff(d) AND (hours(e, d) < hoursSet() OR NOT hours(e, d)) DO {
                    messageManager(employee) <- CONCAT '', messageManager(employee), toDateDDMMYY(d), ', ', timeShortageString(employee, d), '<br />';
                }
                messageManager(employee) <- CONCAT '', messageManager(employee), '<br />';
            }
            
            LOCAL body = STRING ();
            body() <- 'Здравствуйте!<br /><br />Пожалуйста, проверьте со следующими сотрудниками заполняемость трудозатрат.<br /><br />В пердыдущий период следующие сотрудники внесли недостаточно трудозатрат:<br /><br />';
            body() <- CONCAT '', body(), (GROUP CONCAT messageManager(Employee employee), '' IF managers(employee, e));
            body() <- CONCAT '', body(), 'Спасибо,<br /> Ваша CRM система';
            
            EMAIL
                SUBJECT 'Еженедельное информирование руководителя'
                TO email(e)
                BODY body()
            ;
        }
    }
}

EXTEND FORM options
    PROPERTIES sendNotificationsManager() DRAW e TOOLBAR
;